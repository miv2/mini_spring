plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'co.kr'
version = '0.0.1-SNAPSHOT'
description = 'mini_spring'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator' // 헬스 체크용 추가
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client' // OAuth2 클라이언트 의존성 추가
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0') {
        exclude group: 'org.springdoc', module: 'springdoc-openapi-data'
        exclude group: 'org.springdoc', module: 'springdoc-openapi-querydsl'
    }

    // Jackson
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // MariaDB
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // Logging
    implementation 'org.slf4j:slf4j-api'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// JUnit 5 테스트 설정
tasks.named('test') {
    useJUnitPlatform()
    // 테스트 실행 시 상세 로그 출력
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

// Querydsl Q-Class 생성 경로 설정
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

sourceSets {
    main {
        java {
            srcDirs += querydslDir
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(querydslDir)
}

clean {
    delete querydslDir
}

